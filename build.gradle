buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath("com.bmuschko:gradle-docker-plugin:2.6.7")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

def appVersion = project.getProperty("appVersion")

jar {
    baseName = "helloworld"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
}

docker {
    url = 'http://docker-api:2375'
}

task copyTask(type: Copy) {
    from 'src/main/resources/docker/Dockerfile'
    into 'build/libs'
}

task buildImage(type: DockerBuildImage) {
    dependsOn build, copyTask
    inputDir = file('build/libs')
    tag = "helloworld:" + appVersion
}
